<!DOCTYPE html>
<html>
<!-- prettier-ignore -->
<script id="vertex-shader" type="x-shader/x-vertex">#version 300 es

in vec4 vPosition;
in vec3 vNormal;
in vec2 vTexCoord;

out vec2 fTexCoord;
out vec4 fColorFromVertex;

uniform mat4 u_view;
uniform mat4 u_projection;
uniform mat4 u_world;
uniform mat4 u_normal;
uniform mat4 modelMatrix;

out vec3 ts_light_pos;
out vec3 ts_view_pos;
out vec3 ts_frag_pos;

mat3 transposeC(in mat3 inMatrix) {
    vec3 i0 = inMatrix[0];
    vec3 i1 = inMatrix[1];
    vec3 i2 = inMatrix[2];

    mat3 outMatrix = mat3(
        vec3(i0.x, i1.x, i2.x),
        vec3(i0.y, i1.y, i2.y),
        vec3(i0.z, i1.z, i2.z)
    );

    return outMatrix;
}


void main() {
  gl_Position = u_projection * u_view * vPosition;

  ts_frag_pos = vec3(modelMatrix * vPosition);
  vec3 vert_norm = normalize(mat3(u_normal) * vNormal);
  vec3 up = vec3(0.0, 1.0, 0.0);

  vec3 tangent = normalize(cross(up, vert_norm));
  vec3 bitangent = normalize(cross(vert_norm, tangent));

  mat3 tbn = mat3(tangent, bitangent, vert_norm);

  vec3 light_pos = vec3(-100.0, 20.0, 100.0);
  ts_light_pos = tbn * light_pos;

  ts_view_pos = tbn * vec3(0, 0, 0);
  ts_frag_pos = tbn * ts_frag_pos;
  
  // Pass the texture coord to the fragment shader.
  fTexCoord = vTexCoord;
}
</script>

<!-- prettier-ignore -->
<script id="fragment-shader" type="x-shader/x-fragment">#version 300 es

precision mediump float;

uniform sampler2D texture_diffuse;
uniform sampler2D texture_normal;

in vec2 fTexCoord;
in vec3 ts_light_pos;
in vec3 ts_view_pos;
in vec3 ts_frag_pos;

out vec4 fragColor;

void main() {
    vec3 light_dir = normalize(ts_light_pos - ts_frag_pos);
    vec3 view_dir = normalize(ts_view_pos - ts_frag_pos);

    vec2 uv = fTexCoord;

    vec3 albedo = texture(texture_diffuse, uv).rgb;
    vec3 ambient = 0.4 * albedo;

    vec3 norm = normalize(texture(texture_normal, uv).rgb * 2.0 - 1.0);
    float diffuse = max(dot(light_dir, norm), 0.0);
    fragColor = vec4(diffuse * albedo + ambient, 1.0);
}
</script>

<!-- prettier-ignore -->
<script id="fragment-shader-water" type="x-shader/x-fragment">#version 300 es
precision mediump float;

in vec2 fTexCoord;

uniform float time; // Time uniform for animation

const vec2 resolution = vec2(0.9, 0.9);

out vec4 fragColor;

float random(in vec2 uv) {
    return fract(sin(dot(uv.xy, 
                         vec2(12.9898, 78.233))) * 
                 43758.5453123);
}

float noise(in vec2 uv)
{
    vec2 i = floor(uv);
    vec2 f = fract(uv);
    f = f * f * (3. - 2. * f);
    
    float lb = random(i + vec2(0., 0.));
    float rb = random(i + vec2(1., 0.));
    float lt = random(i + vec2(0., 1.));
    float rt = random(i + vec2(1., 1.));
    
    return mix(mix(lb, rb, f.x), 
               mix(lt, rt, f.x), f.y);
}

float seaOctave(vec2 uv, float choppy) 
{	
    float noise = noise(uv);
    float x = cos(noise);
    float y = sin(noise);
   	return pow(pow(abs(x * y), 0.65), choppy);
}

float f(vec3 p) 
{
    vec2 uv = p.xz * vec2(0.85, 1.0); 
    
    float freq 	 = 1.;
    float amp    = .5;  
    float choppy = 7.;
    
    float gSeaCurrentTime = time * 0.3;
	
    float d = 0.0;
    float h = 0.0;    
    for(int i = 0; i < 20; ++i) 
    {	    
    	d =  seaOctave((uv + gSeaCurrentTime) * freq, choppy);
		d += seaOctave((uv - gSeaCurrentTime) * freq, choppy); 
		h += d * amp;
	
		freq *= 2.; 
		amp  *= .2;
	
		uv *= mat2(1.6, 1.2, -1.2, 1.6);
    }
    return h;
}

vec3 getNormal(vec3 p, float t)
{ 
    vec3 eps=vec3(.001 * t, .0, .0);
    vec3 n=vec3(f(p - eps.xyy) - f(p + eps.xyy),
                2. * eps.x,
                f(p - eps.yyx) - f(p + eps.yyx));
  
   	//return vec3(0., 1., 0.);
    return normalize(n);
    
}

vec3 sun(vec3 rd, vec3 lightDir)
{
    vec3 col = vec3(0.);
    
    float sundot = clamp(dot(rd, lightDir), 0.0, 1.0);
    col += 0.25*vec3(1.0,0.7,0.4)*pow( sundot,5.0 );
    col += 0.25*vec3(1.0,0.8,0.6)*pow( sundot,64.0 );
   	col += 0.2*vec3(1.0,0.8,0.6)*pow( sundot,512.0 );
    
    return col;
}

vec3 sky(vec3 rd, vec3 lightDir)
{
    vec3 col = vec3(0.3,0.5,0.85) - rd.y*rd.y*0.5;
    col = mix( col, 0.85*vec3(0.7,0.75,0.85), pow( 1.0-max(rd.y,0.0), 4.0 ) );
    
    // horizon
    col = mix( col, 0.68*vec3(0.4,0.65,1.0), pow( 1.0-max(rd.y,0.0), 16.0 ) );
    
    return col;
}

float fresnel(vec3 N, vec3 V)
{
    float F0 = 0.04;
    
    return F0 + (1. - F0) * pow(1. - dot(V, N), 5.);
}

vec3 lighting(vec3 N, vec3 L, vec3 V)
{
    vec3 R = normalize(reflect(-L, N));
    
    float spec = max(dot(R, V), 0.);
    spec = pow(spec, 60.);
    spec = clamp(spec, 0., 1.);
    
    float fresnel = fresnel(N, V);
    
    vec3 reflected = sky(reflect(-V, N), L);
    vec3 refracted = vec3(.059, .059, .235);	// ocean color
    
    vec3 col = mix(refracted, reflected, fresnel);
    col += vec3(spec) ;
    
    return clamp(col, 0., 1.);
}

float rayMarching(in vec3 ro, in vec3 rd, float tMin, float tMax)
{
    if (rd.y > .0)
    {
        return tMax + .1;
    }
    else
    {
        return abs(ro.y / rd.y);
    }
}

mat3 lookAt(vec3 origin, vec3 target, float roll)
{
    vec3 rr = vec3(sin(roll), cos(roll), 0.0);
    vec3 ww = normalize(target - origin);
    vec3 uu = normalize(cross(ww, rr));
    vec3 vv = normalize(cross(uu, ww));

    return mat3(uu, vv, ww);
}

void main() {
    vec2 uv = fTexCoord.xy / resolution.xy;
    uv -= 0.5;
    uv.x *= resolution.x / resolution.y;
    
    vec3 camPos = vec3(0., 1., 0.);
    vec3 camTarget = vec3(4, 0, 0);
    
    mat3 mat = lookAt(camPos, camTarget, 0.0);
    
    vec3 ro = camPos;
    vec3 rd = normalize(mat * vec3(uv.xy, 1.0));
    
    float tMin = .1;
    float tMax = 100.;
    float t = rayMarching(ro, rd, tMin, tMax);
    
    vec3 col = vec3(0.);
    
    vec3 lightDir = normalize(vec3(10., 1, 0.));
    
    if (rd.y > 0.)
    {
        // sky
        col = sky(rd, lightDir);
        col += sun(rd, lightDir);
    }
    else
    {
        // Ocean lighting
        vec3 p = ro + rd * t;
        vec3 normal = getNormal(p, t); 
        
        col = lighting(normal, lightDir, -rd);
        
    }
    
    fragColor = vec4(col,1.0);
}
    
</script>

<script type="text/javascript" src="Common/webgl-utils.js"></script>
<script type="text/javascript" src="Common/initShaders.js"></script>

<script type="text/javascript" src="Common/MV.js"></script>
<script type="text/javascript" src="objects.js"></script>
<script type="text/javascript" src="kyle.js"></script>
<script type="text/javascript" src="main.js"></script>

<style>
  .ViewButton {
    width: 8em;
    height: 3em;
    margin-bottom: 0.25em;
  }
</style>

<body>
  <canvas id="gl-canvas" width="512" height="512">
    Oops ... your browser doesn't support the HTML5 canvas element
  </canvas>

  <br />

  <div id="sliderX">
    X-Roll -180
    <input id="sliderXi" type="range" min="-180" max="180" step="2" value="0" />
    180
    <input id="reset-x" type="button" value="Reset" />
  </div>
  <br />

  <div id="sliderY">
    Y-Roll -180
    <input id="sliderYi" type="range" min="-180" max="180" step="2" value="0" />
    180
    <input id="reset-y" type="button" value="Reset" />
  </div>
  <br />

  <div id="sliderZ">
    Z-Roll -180
    <input id="sliderZi" type="range" min="-180" max="180" step="2" value="0" />
    180
    <input id="reset-z" type="button" value="Reset" />
  </div>
  <br />

  <input id="animToggleButton" type="button" value="Toggle Animation" />
  <input id="reset-all" type="button" value="Reset Camera" />
  <input id="reset-time" type="button" value="Reset Time" />
  <br />
  <br />

  <input class="ViewButton" id="view-back-left" type="button" value="BACK LEFT" />
  <input class="ViewButton" id="view-back" type="button" value="BACK" />
  <input class="ViewButton" id="view-back-right" type="button" value="BACK RIGHT" />
  <br />
  <input class="ViewButton" id="view-left" type="button" value="LEFT" />
  <input class="ViewButton" id="view-top" type="button" value="TOP" />
  <input class="ViewButton" id="view-right" type="button" value="RIGHT" />
  <br />
  <input class="ViewButton" id="view-front-left" type="button" value="FRONT LEFT" />
  <input class="ViewButton" id="view-front" type="button" value="FRONT" />
  <input class="ViewButton" id="view-front-right" type="button" value="FRONT RIGHT" />

  <br />
  <br />

  <div>
    FPS:
    <span id="fps">0</span>
  </div>
</body>

</html>